plugins {
    id 'io.franzbecker.gradle-lombok' version '1.8'
}

group 'systems.whitestar'
version '1.0'

apply plugin: 'java'
apply plugin: 'war'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

configurations {
    jruby
}

ext {
    gemsDir = file('.jruby/gems')
}

task installGems(type: JavaExec) {
    outputs.dir gemsDir

    classpath = configurations.jruby
    main = "org.jruby.Main"
    args "-S gem install -i $gemsDir --no-rdoc --no-ri compass ".tokenize()

    doFirst {
        gemsDir.mkdirs()
    }
}

war {
    archiveName 'ws-www.war'
}

ext {
    cssDir = file("src/main/webapp/assets/css/")
    sassDir = file("src/main/webapp/assets/sass/")
    imagesDir = file("src/main/webapp/assets/images/")
    javascriptsDir = file("src/main/webapp/assets/js/")
}

task compileSass(type: JavaExec) {
    outputs.dir cssDir
    inputs.files installGems
    inputs.dir sassDir
    inputs.dir imagesDir
    inputs.dir javascriptsDir

    classpath = configurations.jruby
    main = "org.jruby.Main"
    args "-S compass compile --sass-dir $sassDir --css-dir $cssDir --images-dir $imagesDir --javascripts-dir $javascriptsDir --relative-assets".tokenize()
    environment 'GEM_PATH', gemsDir
    environment 'PATH', "$gemsDir/bin"

    doFirst {
        cssDir.mkdirs()
    }
}

lombok {
    sha256 = "" // skip verify Lombok task
}

dependencies {
    // Sass & Build
    jruby 'org.jruby:jruby-complete:9.1.14.0'

    // Logging
    compile group: 'log4j', name: 'log4j', version: '1.2.17'
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.25'

    // WebServer
    compile group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: '2.0.1'
    compile group: 'org.glassfish.jersey.containers', name: 'jersey-container-servlet', version: '2.25.1'
    providedCompile 'javax.servlet:javax.servlet-api:3.1.0'

    // Web Requests (Outbound)
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.1'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.3'

    // Vault
    compile group: 'com.bettercloud', name: 'vault-java-driver', version: '3.0.0'

    // Twig
    compile "org.jtwig:jtwig-web:5.86.1.RELEASE"

    // AWS Services
    compile group: 'com.amazonaws', name: 'aws-java-sdk-sns', version: '1.11.233'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}
